import com.tylerkindy.github.Deps
import com.tylerkindy.github.ResolveAllDependenciesTask

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath Deps.androidGradle
        classpath Deps.kotlinGradle
        classpath Deps.apolloGradle
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.20.0"
}

subprojects {
    configurations {
        ktlint
    }

    dependencies {
        ktlint Deps.ktlint
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.github.shyiko.ktlint.Main"
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.github.shyiko.ktlint.Main"
        args "-F", "src/**/*.kt"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    task resolveAllDependencies(type: ResolveAllDependenciesTask)
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
